plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'com.google.devtools.ksp' version '1.5.21-1.0.0-beta06'
    id 'idea'
}

android {
    compileSdk 31
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "com.kz.anreadx"
        minSdk 24
        targetSdk 31
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
        useIR = true
        allWarningsAsErrors = true
        freeCompilerArgs += "-Xopt-in=kotlin.RequiresOptIn"
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion compose_version
    }
}

repositories {
    maven { url 'https://jitpack.io' }
}

dependencies {
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.7'
}

dependencies {
    implementation project(":XmlParser")
    implementation "androidx.compose.material:material:$compose_version"

    implementation 'androidx.activity:activity-compose:1.3.1'

    implementation "androidx.navigation:navigation-compose:2.4.0-alpha09"

    def accompanist_version = "0.18.0"
    implementation "com.google.accompanist:accompanist-swiperefresh:$accompanist_version"
    implementation "com.google.accompanist:accompanist-navigation-animation:$accompanist_version"

    implementation 'io.coil-kt:coil-compose:1.3.2'

    implementation ('org.kodein.di:kodein-di-framework-compose:7.8.0') {
        exclude group: 'androidx.appcompat'
    }

    def room_version = "2.4.0-alpha04"
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    ksp "androidx.room:room-compiler:$room_version"

    implementation "androidx.startup:startup-runtime:1.1.0"

    implementation 'com.jakewharton.threetenabp:threetenabp:1.3.1'

    implementation 'com.squareup.okhttp3:okhttp:4.9.1'
    implementation "com.squareup.okhttp3:logging-interceptor:4.9.1"
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'

    implementation "com.gitlab.mvysny.konsume-xml:konsume-xml:1.0"

    implementation "com.github.haroldadmin:NetworkResponseAdapter:4.2.1"

    implementation "com.github.kzaemrio.FlowStore:annotation:0.0.3"
    ksp("com.github.kzaemrio.FlowStore:compiler:0.0.3")
}

afterEvaluate {
    idea {
        android.sourceSets.forEach { sourceSet ->
            sourceSet.kotlin {
                srcDir("build/generated/ksp/${sourceSet.name}/kotlin")
            }
        }
    }
}
